plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'de.undercouch.download' version '5.3.0'
    id 'java'
    id 'scala'
    id 'application'
    id 'distribution'
}

application {
    mainClass = 'io.snyk.woof.server.WoofStartup'
}

sourceCompatibility = '11'
targetCompatibility = '11'

dependencies {
    implementation 'org.scala-lang:scala-library:2.12.18'
    implementation 'org.scala-lang:scala-reflect:2.12.18'
    implementation 'org.scala-lang:scala-compiler:2.12.18'
    implementation 'com.datasift.dropwizard.scala:dropwizard-scala-core_2.12:1.3.7-1'

    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'io.dropwizard:dropwizard-assets:1.3.8'
    implementation 'io.dropwizard:dropwizard-core:1.3.8'
    implementation 'io.dropwizard:dropwizard-forms:1.3.8'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    implementation 'org.apache.httpcomponents:httpmime:4.5.7'
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'

    // CWE-22: Path Traversal - better-files
    implementation 'com.github.pathikrit:better-files_2.12:3.8.0'

    // CWE-89: SQL Injection - ScalikeJDBC
    implementation 'org.scalikejdbc:scalikejdbc_2.12:3.4.0'
    implementation 'org.postgresql:postgresql:42.5.1'

    // CWE-79: XSS - ScalaTags
    implementation 'com.lihaoyi:scalatags_2.12:0.7.0'

    // CWE-502: Deserialization - Akka Serialization
    implementation 'com.typesafe.akka:akka-actor_2.12:2.5.23'

    // CWE-917: MVEL Injection
    implementation 'org.mvel:mvel2:2.5.2.Final'

    // CWE-917: SpEL Injection
    implementation 'org.springframework:spring-expression:5.1.9.RELEASE'

    // CWE-90: LDAP Injection - pt.tecnico.dsi:ldap
    implementation 'pt.tecnico.dsi:ldap_2.12:0.4.1'

    runtimeOnly 'javax.xml.bind:jaxb-api:2.2.12'
    runtimeOnly 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    runtimeOnly 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    runtimeOnly 'javax.activation:activation:1.1.1'
}

build.dependsOn shadowJar
distZip.dependsOn shadowJar

task fetchAgent(type: Download) {
    src 'https://static.snyk.io/resources/runtime/agent/java/snyk-java-runtime-agent.zip'
    dest buildDir
    overwrite false
}

task unpackAgent(type: Copy) {
    from zipTree("${buildDir}/snyk-java-runtime-agent.zip")
    into "${buildDir}"
}

task writeConfig() {
    doLast {
        if (!project.hasProperty("projectId")) {
            throw new InvalidUserDataException("\n\nYou must specify a project ID.\n\n" +
                    "Please run `snyk monitor`, collect the id from the results' settings page,\n" +
                    "  then re-run `startWithAgent` using that ID.\n\n" +
                    "For example (you *must* change the projectId!):\n\n" +
                    "   ./gradlew -PprojectId=4567901-2345-6789-0123-45678912345 startWithAgent")
        }

        def output = "projectId=${projectId}\n"

        if (project.hasProperty("verbose") && Boolean.parseBoolean(String.valueOf(project.property("verbose")))) {
            output += "logTo=stderr\n"
        }

        new File("${buildDir}/snyk-java-runtime-agent/snyk-agent.properties")
            .text = output
    }

    outputs.upToDateWhen { false }
}

task start(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    args('server')
}

task startWithAgent(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    jvmArgs("-javaagent:${buildDir}/snyk-java-runtime-agent/snyk-java-runtime-agent.jar")
    args('server')
}

task runExploit(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass

    args('list-zip', "${projectDir}/zip-slip.zip")

    doLast {
        println ""
        println ""
        println "An exploit attempt has been made."
        println ""
        println "If your application is protected, then you should"
        println "  shortly see the warning on https://app.snyk.io/"
    }
}

fetchAgent.dependsOn build
unpackAgent.dependsOn fetchAgent
writeConfig.dependsOn unpackAgent
start.dependsOn build
startWithAgent.dependsOn writeConfig

jar {
    archiveBaseName = 'without-deps'
}

shadowJar {
    mergeServiceFiles()

    archiveBaseName = 'java-woof'
    archiveClassifier = ''
    archiveVersion = ''
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = "8.5"
}
